# Cyclic groups

```agda
module group-theory.cyclic-groups where
```

<details><summary>Imports</summary>

```agda
open import foundation.dependent-pair-types
open import foundation.embeddings
open import foundation.existential-quantification
open import foundation.inhabited-subtypes
open import foundation.propositional-truncations
open import foundation.propositions
open import foundation.universe-levels

open import group-theory.free-groups-with-one-generator
open import group-theory.full-subgroups
open import group-theory.generating-elements-groups
open import group-theory.groups
open import group-theory.homomorphisms-groups
open import group-theory.subgroups-generated-by-elements-groups
```

</details>

## Idea

A [group](group-theory.groups.md) `G` is said to be **cyclic** if it is
generated by a single element, i.e., if there
[exists](foundation.existential-quantification.md) an element `g : G` such that
the evaluation map

```text
  hom-Group G H → H
```

given by `f ↦ f g` is an [embedding](foundation.embeddings.md) for every group
`H`.

## Definitions

### Cyclic groups

#### The standard definition of cyclic groups

```agda
module _
  {l1 : Level} (l2 : Level) (G : Group l1)
  where

  is-cyclic-prop-Group : Prop (l1 ⊔ lsuc l2)
  is-cyclic-prop-Group =
    ∃-Prop
      ( type-Group G)
      ( λ g → (H : Group l2) → is-emb (ev-element-hom-Group G H g))

  is-cyclic-Group' : UU (l1 ⊔ lsuc l2)
  is-cyclic-Group' = type-Prop is-cyclic-prop-Group

  is-prop-is-cyclic-Group : is-prop is-cyclic-Group'
  is-prop-is-cyclic-Group = is-prop-type-Prop is-cyclic-prop-Group

module _
  {l1 : Level} (G : Group l1)
  where

  is-cyclic-Group : UUω
  is-cyclic-Group = {l : Level} → type-Prop (is-cyclic-prop-Group l G)
```

#### The definition where `G` has a generating element

```agda
module _
  {l1 : Level} (G : Group l1)
  where

  has-generating-element-prop-Group : Prop l1
  has-generating-element-prop-Group =
    is-inhabited-subtype-Prop (generating-element-Group G)

  has-generating-element-Group : UU l1
  has-generating-element-Group = type-Prop has-generating-element-prop-Group
```

## Properties

### A group is cyclic if and only if it has a generating element

```agda
module _
  {l1 : Level} (G : Group l1)
  where

  is-cyclic-has-generating-element-Group :
    has-generating-element-Group G → is-cyclic-Group G
  is-cyclic-has-generating-element-Group H =
    apply-universal-property-trunc-Prop H
      ( is-cyclic-prop-Group _ G)
      ( λ (g , u) →
        intro-∃ g
          ( is-emb-ev-element-is-generating-element-Group G g u))
```
