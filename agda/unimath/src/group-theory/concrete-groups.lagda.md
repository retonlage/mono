# Concrete groups

```agda
module group-theory.concrete-groups where
```

<details><summary>Imports</summary>

```agda
open import foundation.0-connected-types
open import foundation.1-types
open import foundation.contractible-types
open import foundation.dependent-pair-types
open import foundation.identity-types
open import foundation.mere-equality
open import foundation.propositional-truncations
open import foundation.propositions
open import foundation.sets
open import foundation.truncated-types
open import foundation.truncation-levels
open import foundation.unit-type
open import foundation.universe-levels

open import group-theory.groups

open import higher-group-theory.higher-groups

open import structured-types.pointed-types
```

</details>

## Idea

A concrete group is a pointed connected 1-type.

## Definition

```agda
Concrete-Group : (l : Level) ‚Üí UU (lsuc l)
Concrete-Group l = Œ£ (‚àû-Group l) (Œª G ‚Üí is-set (type-‚àû-Group G))

module _
  {l : Level} (G : Concrete-Group l)
  where

  ‚àû-group-Concrete-Group : ‚àû-Group l
  ‚àû-group-Concrete-Group = pr1 G

  classifying-pointed-type-Concrete-Group : Pointed-Type l
  classifying-pointed-type-Concrete-Group =
    classifying-pointed-type-‚àû-Group ‚àû-group-Concrete-Group

  classifying-type-Concrete-Group : UU l
  classifying-type-Concrete-Group =
    classifying-type-‚àû-Group ‚àû-group-Concrete-Group

  shape-Concrete-Group : classifying-type-Concrete-Group
  shape-Concrete-Group =
    shape-‚àû-Group ‚àû-group-Concrete-Group

  is-0-connected-classifying-type-Concrete-Group :
    is-0-connected classifying-type-Concrete-Group
  is-0-connected-classifying-type-Concrete-Group =
    is-0-connected-classifying-type-‚àû-Group ‚àû-group-Concrete-Group

  mere-eq-classifying-type-Concrete-Group :
    (X Y : classifying-type-Concrete-Group) ‚Üí mere-eq X Y
  mere-eq-classifying-type-Concrete-Group =
    mere-eq-classifying-type-‚àû-Group ‚àû-group-Concrete-Group

  elim-prop-classifying-type-Concrete-Group :
    {l2 : Level} (P : classifying-type-Concrete-Group ‚Üí Prop l2) ‚Üí
    type-Prop (P shape-Concrete-Group) ‚Üí
    ((X : classifying-type-Concrete-Group) ‚Üí type-Prop (P X))
  elim-prop-classifying-type-Concrete-Group =
    elim-prop-classifying-type-‚àû-Group ‚àû-group-Concrete-Group

  type-Concrete-Group : UU l
  type-Concrete-Group = type-‚àû-Group ‚àû-group-Concrete-Group

  is-set-type-Concrete-Group : is-set type-Concrete-Group
  is-set-type-Concrete-Group = pr2 G

  set-Concrete-Group : Set l
  set-Concrete-Group = pair type-Concrete-Group is-set-type-Concrete-Group

  is-1-type-classifying-type-Concrete-Group :
    is-trunc one-ùïã classifying-type-Concrete-Group
  is-1-type-classifying-type-Concrete-Group X Y =
    apply-universal-property-trunc-Prop
      ( mere-eq-classifying-type-Concrete-Group shape-Concrete-Group X)
      ( is-set-Prop (Id X Y))
      ( Œª { refl ‚Üí
            apply-universal-property-trunc-Prop
              ( mere-eq-classifying-type-Concrete-Group shape-Concrete-Group Y)
              ( is-set-Prop (Id shape-Concrete-Group Y))
              ( Œª { refl ‚Üí is-set-type-Concrete-Group})})

  classifying-1-type-Concrete-Group : Truncated-Type l one-ùïã
  classifying-1-type-Concrete-Group =
    pair
      classifying-type-Concrete-Group
      is-1-type-classifying-type-Concrete-Group

  Id-BG-Set :
    (X Y : classifying-type-Concrete-Group) ‚Üí Set l
  Id-BG-Set X Y = Id-Set classifying-1-type-Concrete-Group X Y

  unit-Concrete-Group : type-Concrete-Group
  unit-Concrete-Group = unit-‚àû-Group ‚àû-group-Concrete-Group

  mul-Concrete-Group : (x y : type-Concrete-Group) ‚Üí type-Concrete-Group
  mul-Concrete-Group = mul-‚àû-Group ‚àû-group-Concrete-Group

  mul-Concrete-Group' : (x y : type-Concrete-Group) ‚Üí type-Concrete-Group
  mul-Concrete-Group' x y = mul-Concrete-Group y x

  associative-mul-Concrete-Group :
    (x y z : type-Concrete-Group) ‚Üí
    Id
      ( mul-Concrete-Group (mul-Concrete-Group x y) z)
      ( mul-Concrete-Group x (mul-Concrete-Group y z))
  associative-mul-Concrete-Group =
    associative-mul-‚àû-Group ‚àû-group-Concrete-Group

  left-unit-law-mul-Concrete-Group :
    (x : type-Concrete-Group) ‚Üí Id (mul-Concrete-Group unit-Concrete-Group x) x
  left-unit-law-mul-Concrete-Group =
    left-unit-law-mul-‚àû-Group ‚àû-group-Concrete-Group

  right-unit-law-mul-Concrete-Group :
    (y : type-Concrete-Group) ‚Üí Id (mul-Concrete-Group y unit-Concrete-Group) y
  right-unit-law-mul-Concrete-Group =
    right-unit-law-mul-‚àû-Group ‚àû-group-Concrete-Group

  coherence-unit-laws-mul-Concrete-Group :
    Id
      ( left-unit-law-mul-Concrete-Group unit-Concrete-Group)
      ( right-unit-law-mul-Concrete-Group unit-Concrete-Group)
  coherence-unit-laws-mul-Concrete-Group =
    coherence-unit-laws-mul-‚àû-Group ‚àû-group-Concrete-Group

  inv-Concrete-Group : type-Concrete-Group ‚Üí type-Concrete-Group
  inv-Concrete-Group = inv-‚àû-Group ‚àû-group-Concrete-Group

  left-inverse-law-mul-Concrete-Group :
    (x : type-Concrete-Group) ‚Üí
    Id (mul-Concrete-Group (inv-Concrete-Group x) x) unit-Concrete-Group
  left-inverse-law-mul-Concrete-Group =
    left-inverse-law-mul-‚àû-Group ‚àû-group-Concrete-Group

  right-inverse-law-mul-Concrete-Group :
    (x : type-Concrete-Group) ‚Üí
    Id (mul-Concrete-Group x (inv-Concrete-Group x)) unit-Concrete-Group
  right-inverse-law-mul-Concrete-Group =
    right-inverse-law-mul-‚àû-Group ‚àû-group-Concrete-Group

  abstract-group-Concrete-Group : Group l
  pr1 (pr1 abstract-group-Concrete-Group) = set-Concrete-Group
  pr1 (pr2 (pr1 abstract-group-Concrete-Group)) = mul-Concrete-Group
  pr2 (pr2 (pr1 abstract-group-Concrete-Group)) = associative-mul-Concrete-Group
  pr1 (pr1 (pr2 abstract-group-Concrete-Group)) = unit-Concrete-Group
  pr1 (pr2 (pr1 (pr2 abstract-group-Concrete-Group))) =
    left-unit-law-mul-Concrete-Group
  pr2 (pr2 (pr1 (pr2 abstract-group-Concrete-Group))) =
    right-unit-law-mul-Concrete-Group
  pr1 (pr2 (pr2 abstract-group-Concrete-Group)) =
    inv-Concrete-Group
  pr1 (pr2 (pr2 (pr2 abstract-group-Concrete-Group))) =
    left-inverse-law-mul-Concrete-Group
  pr2 (pr2 (pr2 (pr2 abstract-group-Concrete-Group))) =
    right-inverse-law-mul-Concrete-Group

  op-abstract-group-Concrete-Group : Group l
  pr1 (pr1 op-abstract-group-Concrete-Group) = set-Concrete-Group
  pr1 (pr2 (pr1 op-abstract-group-Concrete-Group)) = mul-Concrete-Group'
  pr2 (pr2 (pr1 op-abstract-group-Concrete-Group)) x y z =
    inv (associative-mul-Concrete-Group z y x)
  pr1 (pr1 (pr2 op-abstract-group-Concrete-Group)) = unit-Concrete-Group
  pr1 (pr2 (pr1 (pr2 op-abstract-group-Concrete-Group))) =
    right-unit-law-mul-Concrete-Group
  pr2 (pr2 (pr1 (pr2 op-abstract-group-Concrete-Group))) =
    left-unit-law-mul-Concrete-Group
  pr1 (pr2 (pr2 op-abstract-group-Concrete-Group)) = inv-Concrete-Group
  pr1 (pr2 (pr2 (pr2 op-abstract-group-Concrete-Group))) =
    right-inverse-law-mul-Concrete-Group
  pr2 (pr2 (pr2 (pr2 op-abstract-group-Concrete-Group))) =
    left-inverse-law-mul-Concrete-Group
```

## Example

### The trivial concrete group

```agda
trivial-Concrete-Group : {l : Level} ‚Üí Concrete-Group l
trivial-Concrete-Group =
  trivial-‚àû-Group ,
  is-trunc-is-contr
    (succ-ùïã (succ-ùïã (succ-ùïã neg-two-ùïã)))
    is-contr-raise-unit
    raise-star
    raise-star
```
